buildscript {
	ext {
		grailsVersion = project.grailsVersion
	}
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		maven { url 'https://repo.grails.org/grails/core' }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
		classpath "org.asciidoctor:asciidoctor-gradle-jvm:3.1.0"
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:latest.release"
	}
}

plugins {
	id 'io.spring.dependency-management' version '0.5.2.RELEASE'
}

version = file('version.txt').text.trim()
group 'org.grails.plugins'

apply plugin: 'java-library'
apply plugin: 'org.grails.grails-plugin'
apply plugin: 'maven-publish'
apply plugin: "org.asciidoctor.jvm.convert"
apply plugin: "com.jfrog.artifactory"

ext {
	grailsVersion = project.grailsVersion
}

sourceCompatibility = targetCompatibility = 1.8

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://repo.grails.org/grails/core' }
}

dependencyManagement {
	imports {
		mavenBom "org.grails:grails-bom:$grailsVersion"
	}
	applyMavenExclusions false
}

dependencies {
	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
	compileOnly 'org.grails:grails-dependencies'
	compileOnly 'org.grails:grails-web-boot'

	api 'org.grails.plugins:spring-security-core:5.3.0'

	api "org.springframework.security:spring-security-ldap:$springSecurityVersion", {
		['apacheds-core', 'apacheds-core-entry', 'apacheds-protocol-ldap', 'apacheds-protocol-shared',
		 'apacheds-server-jndi', 'commons-logging', 'fest-assert', 'jcl-over-slf4j', 'junit',
		 'ldapsdk', 'logback-classic', 'mockito-core', 'shared-ldap', 'slf4j-api', 'spring-beans',
		 'spring-context', 'spring-core', 'spring-ldap-core', 'spring-security-core',
		 'spring-test', 'spring-tx'].each { exclude module: it }
	}

	api "org.springframework.ldap:spring-ldap-core:$springLdapCoreVersion", {
		['commons-lang', 'gsbase', 'junit', 'mockito-core', 'powermock-api-mockito',
		 'powermock-api-support', 'powermock-core', 'powermock-module-junit4',
		 'powermock-module-junit4-common', 'powermock-reflect', 'slf4j-log4j12', 'spring-beans',
		 'spring-core', 'spring-data-commons', 'spring-test', 'spring-tx'].each { exclude module: it }
	}

	console 'org.grails:grails-console'
	testImplementation "org.grails:grails-web-testing-support"
}

apply from: "${rootProject.projectDir}/gradle/artifactoryPublish.gradle"
apply from: "${rootProject.projectDir}/gradle/mavenPublish.gradle"
apply from: "${rootProject.projectDir}/gradle/testVerbose.gradle"

asciidoctor {
	sources {
		include 'index.adoc'
	}
	outputDir file('build/docs')
	attributes 'source-highlighter': 'prettify',
	            icons:               'font',
	            setanchors:          'true',
	            idprefix:            '',
	            idseparator:         '-',
	            toc2:                '',
	            numbered:            '',
	            revnumber:           project.version
}

task docs(dependsOn: asciidoctor)  {
	group 'documentation'
	doLast() {
		File dir = file('build/docs')
		file('build/docs/ghpages.html') << file('src/docs/asciidoc/index.tmpl').text.replaceAll('@VERSION@', project.version)
	}

}
