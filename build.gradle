plugins {
	id 'io.spring.dependency-management'
	id 'java-library'
	id 'org.grails.grails-plugin'
	id 'maven-publish'
	id "org.asciidoctor.jvm.convert"
	id "com.jfrog.artifactory"
	id "com.gorylenko.gradle-git-properties"
}

version = file('version.txt').text.trim()
group 'org.grails.plugins'

ext {
	grailsVersion = project.grailsVersion
}

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11
	// tested and works fine on Java 17
	//sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://repo.grails.org/grails/core' }
}

dependencyManagement {
	imports {
		mavenBom "org.grails:grails-bom:$grailsVersion"
	}
	applyMavenExclusions false
}

dependencies {
	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'
	compileOnly 'org.grails:grails-dependencies'
	compileOnly 'org.grails:grails-web-boot'

	api 'org.grails.plugins:spring-security-core:6.1.1'

	api "org.springframework.security:spring-security-ldap:$springSecurityVersion", {
		['apacheds-core', 'apacheds-core-entry', 'apacheds-protocol-ldap', 'apacheds-protocol-shared',
		 'apacheds-server-jndi', 'commons-logging', 'fest-assert', 'jcl-over-slf4j', 'junit',
		 'ldapsdk', 'logback-classic', 'mockito-core', 'shared-ldap', 'slf4j-api', 'spring-beans',
		 'spring-context', 'spring-core', 'spring-ldap-core', 'spring-security-core',
		 'spring-test', 'spring-tx'].each { exclude module: it }
	}

	api "org.springframework.ldap:spring-ldap-core:$springLdapCoreVersion", {
		['commons-lang', 'gsbase', 'junit', 'mockito-core', 'powermock-api-mockito',
		 'powermock-api-support', 'powermock-core', 'powermock-module-junit4',
		 'powermock-module-junit4-common', 'powermock-reflect', 'slf4j-log4j12', 'spring-beans',
		 'spring-core', 'spring-data-commons', 'spring-test', 'spring-tx'].each { exclude module: it }
	}

	console 'org.grails:grails-console'
	testImplementation "org.grails:grails-web-testing-support"
}

apply from: "${rootProject.projectDir}/gradle/artifactoryPublish.gradle"
apply from: "${rootProject.projectDir}/gradle/mavenPublish.gradle"
apply from: "${rootProject.projectDir}/gradle/testVerbose.gradle"

asciidoctor {
	sources {
		include 'index.adoc'
	}
	outputDir file('build/docs')
	attributes 'source-highlighter': 'prettify',
	            icons:               'font',
	            setanchors:          'true',
	            idprefix:            '',
	            idseparator:         '-',
	            toc2:                '',
	            numbered:            '',
	            revnumber:           project.version
}

task docs(dependsOn: asciidoctor)  {
	group 'documentation'
	doLast() {
		File dir = file('build/docs')
		file('build/docs/ghpages.html') << file('src/docs/asciidoc/index.tmpl').text.replaceAll('@VERSION@', project.version)
	}

}

tasks.withType(GroovyCompile) {
	configure(groovyOptions) {
		forkOptions.jvmArgs = ['-Xmx1024m']
	}
}

tasks.withType(Test) {
	useJUnitPlatform()
}
// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false


gitProperties {
	keys = ['git.branch', 'git.commit.id', 'git.commit.time', 'git.commit.id.abbrev']
	failOnNoGitDirectory = true
	extProperty = 'gitProps' // git properties will be put in a map at project.ext.gitProps
}

generateGitProperties.outputs.upToDateWhen {
	false
} // make sure the generateGitProperties task always executes (even when git.properties is not changed)

jar {
	dependsOn generateGitProperties
	manifest {
		attributes("Built-By": System.getProperty("user.name"))
		attributes(["Plugin-Version"        : version,
					"Plugin-Title"          : project.name,
					"Plugin-Build-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"Git-Commit"            : "${-> project.ext.gitProps['git.commit.id.abbrev']}",
					"Git-Branch"            : "${-> project.ext.gitProps['git.branch']}"])
	}
	from sourceSets.main.output
	exclude 'git.properties'
}
